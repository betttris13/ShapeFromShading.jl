@doc raw"""
```
img = generate_surface(shape::SynthShape, albedo::Real = 0.5, illumination_direction::Vector{T} where T <: Real = [0, 0, 1]; radius::Real = 50, img_size::Int=151, noise::Real = 0, relative_noise::Bool = false, background::Bool = true)
```
Creates a synthetic image of a given shape with the given illumination properties.
# Output
Returns a grayscale image with dimensions `img_size` in both axes with background
ilumination determaned by `background`.
# Details
Uses the passed properties to calculate the partial differentials of the shape
reflectance and uses these to generate a synthetic image of the shape to be used
as a consistent test image for SFS testing.
# Arguments
The function arguments are described in more detail below.
## `shape`
A `SynthShape` that specifies the type of shape to be generated. Available shapes
are; SynthSphere, Ripple, Ripple2, Cake, Cake2, Cup, SynthVase, Tent, Dem,
SynthGaussian and SuperGaussian.
## `albedo`
A `Real` that specifies the albedo (amount of light reflected) of the image. If
left unspecified a default value of 0.5 is used.
## `illumination_direction`
A `Vector{T} where T <: Real` that specifies the tilt value to be used by the
algorithm. The `illumination_direction` should be a vector of the form [x,y,z]
where x,y,z are in the range [0,1]. If left unspecified a default value of
[0,0,1] is used.
## `radius`
A `Real` that specifies the radius of the desired shape or its scale within the
image. This can acts as either an actual radius (such as for `SynthSphere`) or
rather as a scale factor (such as for `Ripple`). If left unspecified a default
value is used.
## `img_size`
A `Int` that specifies the dimentions of the final image should be. Defults to 151.
## `noise`
A `Real` that specifies the standard deviation of noise to be added to the image.
Defults to 0.0.
## `relative_noise`
A `Bool` that specifies if the noise added to the image should be relative to the
gradient of the object displayed in the image at that point.
## `backgorund`
A `Bool` that specifies if the background should be illuminated. This can produce
unexpencted results on some shapes.
# Example
Compute the heightmap for a synthetic image generated by `generate_surface`.
```julia
using Images, ShapeFromShading

#generate synthetic image
img = generate_surface(SynthSphere(), 0.5, [0.5,0.1,0.7], radius = 50)
```
# Reference
1. S. Elhabian, "Hands on Shape from Shading", Computer Vision and Image Processing, 2008.
"""
function generate_surface(shape::SynthShape, albedo::Real = 0.5, illumination_direction::Vector{T} where T <: Real = [0, 0, 1]; radius::Real = 50, img_size::Int=151, noise::Real = 0, relative_noise::Bool = false, background::Bool = true)
    I = illumination_direction./norm(illumination_direction)
    ρ = albedo
    R = zeros(Float64, img_size, img_size)
    x,y = setup_xy(img_size)
    p,q = sythetic_gradient(shape, radius=radius, img_size=img_size, noise=noise, relative_noise=relative_noise)
    for i in CartesianIndices(R)
        R[i] = (ρ * (-I[1] * p[i] - I[2] * q[i] + I[3])) / sqrt(1 + p[i]^2 + q[i]^2)
        if shape([x[i],y[i]], radius) <= 0 && background == false
            R[i] = 0.0
        end
    end
    E = max.(0.0, Float64.(R))

    #convert to img and return
    img = Gray.(E)
    return img
end

# Generates image from given gradients
function generate_surface(p, q, albedo::Real = 0.5, illumination_direction::Vector{T} where T <: Real = [0, 0, 1]; radius::Real = 50, img_size::Int=151, noise::Real = 0, relative_noise::Bool = false, background::Bool = true)
    I = illumination_direction./norm(illumination_direction)
    ρ = albedo
    R = zeros(Float64, img_size, img_size)
    x,y = setup_xy(img_size)
    for i in CartesianIndices(R)
        R[i] = (ρ * (-I[1] * p[i] - I[2] * q[i] + I[3])) / sqrt(1 + p[i]^2 + q[i]^2)
    end
    E = max.(0.0, Float64.(R))

    #convert to img and return
    img = Gray.(E)
    return img
end

# Generates image, gradients and ground truth for a shape
function generate_surface_data(shape::SynthShape, albedo::Real = 0.5, illumination_direction::Vector{T} where T <: Real = [0, 0, 1]; radius::Real = 50, img_size::Int=151, noise::Real = 0, relative_noise::Bool = false, background::Bool = true)
    img = generate_surface(shape, albedo, illumination_direction, radius = radius, img_size = img_size, noise = noise, relative_noise = relative_noise, background = background)
    p,q = sythetic_gradient(shape, radius = radius, img_size = img_size, noise = noise, relative_noise = relative_noise)
    Z = ground_truth(shape, radius = radius, img_size = img_size)
    return img, p, q, Z
end

# Generate three sythetic images of a shape
function generate_photometric(shape::SynthShape, albedo::Real = 0.5, I₁::Vector{T} where T <: Real = [0, 0, 1], I₂::Vector{T} where T <: Real = [0.5, 0, 1], I₃::Vector{T} where T <: Real = [0, 0.5, 1]; radius::Real = 50, img_size::Int=151, noise::Real = 0, relative_noise::Bool = false, background::Bool = true)
    img1 = generate_surface(shape, albedo, I₁, radius = radius, img_size = img_size, noise = noise, relative_noise = relative_noise, background = background)
    img2 = generate_surface(shape, albedo, I₂, radius = radius, img_size = img_size, noise = noise, relative_noise = relative_noise, background = background)
    img3 = generate_surface(shape, albedo, I₃, radius = radius, img_size = img_size, noise = noise, relative_noise = relative_noise, background = background)
    return img1, img2, img3
end

# Generates synthetic gradients for a shape
function sythetic_gradient(shape::SynthShape; radius::Real = 50, img_size::Int=151, noise::Real = 0, relative_noise::Bool = false)
    od = OnceDifferentiable(x -> shape(x, Float64(radius)), [0.0,0.0]; autodiff = :forward)
    p = zeros(Float64, img_size, img_size)
    q = zeros(Float64, img_size, img_size)
    x,y = setup_xy(img_size)
    for i in CartesianIndices(x)
            pq = [0.0,0.0]
            od.df(pq,([x[i],y[i]]))
            p[i], q[i] = pq
            if isnan(p[i])
                p[i] = 0.0
            end
            if isnan(q[i])
                q[i] = 0.0
            end
            if relative_noise == false
                p[i] += rand(Normal(0, noise))
                q[i] += rand(Normal(0, noise))
            else
                p[i] += p[i]*rand(Normal(0, noise))
                q[i] += q[i]*rand(Normal(0, noise))
            end
    end
    return p,q
end

# Generates gorund truth for a shape
function ground_truth(shape::SynthShape; radius::Real = 50, img_size::Int=151)
    Z = zeros(Float64, img_size, img_size)
    x,y = setup_xy(img_size)
    for i in CartesianIndices(Z)
        Z[i] = shape([x[i],y[i]], radius)
    end
    return Z
end

# Sphere
function (shape::SynthSphere)(x::AbstractArray, r::Real)
    if x[1]^2 + x[2]^2 < r^2
        z = sqrt(r^2-(x[1]^2 + x[2]^2))
    else
        z = 0.0
    end
    return z
end

# Cake like shape
function (shape::Cake)(x::AbstractArray, r::Real)
    z = 75 * sin(π*(x[1]^2 + x[2]^2) / (71 * r))
    if z < 0.0
        z = 0.0
    end
    if x[1]^2 + x[2]^2 > r^2
        z = 0.0
    end
    return z
end

# Cake like shape version 2
function (shape::Cake2)(x::AbstractArray, r::Real)
    xy = sqrt(x[1]^2 + x[2]^2)
    z = -(xy + 60)*(xy + 20)*(xy - 20)*(xy - 60) / 60000
    if z < 0.0
        z = 0.0
    elseif z > 30.0
        z = 30.0
    end
    return z
end

# Ripple
function (shape::Ripple)(x::AbstractArray, r::Real)
    z = 25*cos(sqrt(x[1]^2 + x[2]^2) / r)
    return z
end

# Ripple with decayed edges
function (shape::Ripple2)(x::AbstractArray, r::Real)
    z = 25 * cos(sqrt(x[1]^2 + x[2]^2)/r) * 62.5 * (1/sqrt(pi * 1250)) * exp(-((x[1]^2 + x[2]^2) / 1250))
    return z
end

# Cup
function (shape::Cup)(x::AbstractArray, r::Real)
    if abs(x[1] + 20) < 35.0 && abs(x[2]) < 45
        z = sqrt(35^2 - (x[1] + 20)^2)
    elseif (x[1]) >= 15 && 25-(sqrt(x[2]^2+(x[1] - 10)^2)-25)^2 > 0
        z = 25-(sqrt(x[2]^2+(x[1] - 10)^2)-25)^2
    else
        z = 0.0
    end
    return z
end

function f(x)
    return -138.24 * x^6 + 92.16 * x^5 + 84.48 * x^4 - 48.64 * x^3 - 17.60 * x^2 + 6.4 * x + 3.2
end

# Vase
function (shape::SynthVase)(x::AbstractArray, r::Real)
    x = x./r .* 6.4
    if f(x[1]/12.8)^2 >= x[2]^2
        z = sqrt(f(x[1]/12.8)^2 - x[2]^2)*r/6.4
    else
        z = 0.0
    end
    return z
end

# Canadian tent
function (shape::Tent)(x::AbstractArray, r::Real)
    x = x ./ r .* 6.4
    if abs(x[1]) <= 5.12 && abs(x[2]) <= 5.12
        z = min(-2 * abs(x[1]) + 10.24, -abs(x[2]) + 5.12)*r/6.4
    else
        z = 0.0
    end
    return z
end

function g(x,y)
    return 3 * (1 - x)^2 * exp(-x^2 - (y + 1)^2) - 10 * (x / 5 - x^3 - y^5) * exp(-x^2 - y^2) - (1/3) * exp(-(x + 1)^2 - y^2)
end

# DEM
function (shape::Dem)(x::AbstractArray, r::Real)
    x = x ./ r .* 6.4
    x = x ./ 1.6
    z = g(x[1], x[2]) *r/6.4
    return z
end

# Gaussian
function (shape::SynthGaussian)(x::AbstractArray, r::Real)
    x = x ./ r .* 4
    z = exp(-((x[1]^2)/2+(x[2]^2)/2)) * r/4
    return z
end

# Gaussian to a higher power
function (shape::SuperGaussian)(x::AbstractArray, r::Real)
    x = x ./ r .* 4
    P = 2
    z = exp(-((x[1]^2)/2+(x[2]^2)/2)^P)*r/4
    return z
end

# Allows for interactive generation of images
function createImage()
    slider1 = widget(-1:0.01:1)
    slider2 = widget(-1:0.01:1)
    slider3 = widget(0.01:0.01:1)
    slider4 = widget(1:100, value = 5)
    sizeVal = spinbox(1:5000, label="Image size:", value = 151)
    r = slider4[]
    I = [slider1[],slider2[],slider3[]]
    imgSize = sizeVal[]
    img = generate_surface(SynthSphere(), 1, I, radius = 5)
    dis = Observable(img)
    shape = SynthSphere()
    shapeOptions = dropdown(OrderedDict("Sphere"=>SynthSphere(), "Ripple"=>Ripple(), "Ripple2"=>Ripple2(), "Cake"=>Cake(), "Cake2"=>Cake2(), "Vase"=>SynthVase(), "Dem"=>Dem(), "Tent"=>Tent(), "Gaussian"=>SynthGaussian(), "Super Gaussian"=>SuperGaussian()))
    ui = Interact.vbox(slider1,slider2,slider3, dis, Interact.hbox(shapeOptions, sizeVal),slider4)
    w = Window()
    body!(w,ui)

    on(slider1) do val
        I = [slider1[],slider2[],slider3[]]
        img = generate_surface(shape, 1.0, I, radius = r, img_size = imgSize)
        dis[] = img
    end

    on(slider2) do val
        I = [slider1[],slider2[],slider3[]]
        img = generate_surface(shape, 1.0, I, radius = r, img_size = imgSize)
        dis[] = img
    end

    on(slider3) do val
        I = [slider1[],slider2[],slider3[]]
        img = generate_surface(shape, 1.0, I, radius = r, img_size = imgSize)
        dis[] = img
    end

    on(slider4) do val
        r = slider4[]
        img = generate_surface(shape, 1.0, I, radius = r, img_size = imgSize)
        dis[] = img
    end

    on(shapeOptions) do val
        shape = shapeOptions[]
        img = generate_surface(shape, 1.0, I, radius = r, img_size = imgSize)
        dis[] = img
    end

    on(sizeVal) do val
        imgSize = sizeVal[]
        img = generate_surface(shape, 1.0, I, radius = r, img_size = imgSize)
        dis[] = img
    end
end
